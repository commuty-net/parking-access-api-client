plugins {
    id "idea"
    id "java-library"
    id "maven-publish"
    id "groovy"
}

group "net.commuty"
version "0.1"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

compileJava {
    exclude "module-info.java"
    options.compilerArgs = ["--release", "8"]
}

task compileModuleInfoJava(type: JavaCompile) {
    classpath = files()
    source = "src/main/java/module-info.java"
    destinationDir = compileJava.destinationDir
    doFirst {
        options.compilerArgs = [
                "--release", "9",
                "--module-path", compileJava.classpath.asPath,
        ]
    }
}

compileModuleInfoJava.dependsOn compileJava
classes.dependsOn compileModuleInfoJava

jar {
    archiveBaseName = "parking-access-api-client"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

build.finalizedBy publishToMavenLocal

dependencies {
    implementation (
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion",
            "com.fasterxml.jackson.module:jackson-module-paranamer:$jacksonVersion",
            "org.slf4j:slf4j-api:$slf4jVersion"
    )

    testImplementation (
            "org.spockframework:spock-core:$spockVersion",
            "org.codehaus.groovy:groovy-all:$groovyVersion",
    )
}
